grammar com.andredidier.multilingualtexteditor.MultilingualTextEditor hidden (WS)
//with org.eclipse.xtext.common.Terminals 

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate multilingualTextEditor "http://www.andredidier.com/multilingualtexteditor/MultilingualTextEditor"

Text:
	(reviewers+=Reviewer)*
	(reviews+=Review)*
	(languages+=Language)*
	(models+=Model)*
	(elements+=Element)*
	(textualContents+=TextualContent)*
;

//terminal LC  		: '^'?('a'..'z') ('a'..'z'|'0'..'9')*;
//terminal UC  		: '^'?('A'..'Z') ('A'..'Z'|'0'..'9')*;
terminal ID  		: '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
//terminal ID  		: ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
terminal STRING	: 
			'"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'"') )* '"' |
			"'" ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|"'") )* "'";
terminal WS			: (' '|'\t'|'\r'|'\n')+;
terminal ANY_OTHER: .;

Reviewer:
	'reviewer'
	name=ID
	'name' fullName=STRING
;

Review:
	'review'
	name=ID
	responsible=[Reviewer]
	date=STRING
;

Element:
	'element' name=ID 
	'{'
		plainText=PlainTextConfig? & 
		wordConfig=WordConfig? &
		htmlConfig=HTMLConfig? &
		mdConfig=MDConfig? &
		('child' subelements+=Element)*
	'}' 
;

PlainTextConfig returns TextualConfig:
	'plain' (hide?='hide' | (prefix=STRING ('text' suffix=STRING)?)) 
;

WordConfig:
	'word' styleName=STRING 
;

HTMLConfig:
	'html' (body?='body' | header?='header' | footer?='footer') htmlElement=STRING htmlClassNames+=STRING* 
;

MDConfig returns TextualConfig:
	'md' (hide?='hide' | (prefix=STRING ('text' suffix=STRING)?)) 
;

Language:
	'language' name=ID code=ID country=CountryCode? 
;

Model:
	'model' name=ID 
;

CountryCode:
	name=ID variantCode=ID?
;

TextualContent:
	('(' ( models+=[Model] )* ')' | hiddenContent?='hidden')?
	element=[Element] 
	(
		'{' (children+=TextualContent)+ '}' | 
		values+=LocalizedText+
	)
	
;

LocalizedText:
	'=>' language=[Language]  
	((values+=Sentence)+ | hiddenContent?='suppressed'?)
;

Sentence:
	{Sentence}
	('replace' replaceReview=[Review] 'original' original=STRING)?
	('delete' deletionReview=[Review])?
	('insert' insertionReview=[Review])?
	(
		modifier+='bold'? &
		modifier+='italics'? &
		modifier+='underline'? &
		nodic?='nodic'?
		
	)
	value=STRING
;


